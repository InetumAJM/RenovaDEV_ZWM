FUNCTION ZWM_LM_MATERIAL_MASTER_DISPLAY.
*"----------------------------------------------------------------------
*"*"Interface local:
*"  TABLES
*"      CT_WORKTAB STRUCTURE  ZWMOV
*"  EXCEPTIONS
*"      NO_ITEM_DATA
*"      NO_PERMISSION
*"----------------------------------------------------------------------
  DATA: BEGIN OF LT_SELECT OCCURS 10,
          MATNR LIKE ZWMOV-MATNR,
          VKORG LIKE ZWMOV-VKORG,
          VTWEG LIKE ZWMOV-VTWEG,
        END OF LT_SELECT,
        LF_FLAG_ITEMS TYPE C,
        LF_ANSWER TYPE C,
        LF_TEXTLINE1(50) TYPE C,
        LF_TEXTLINE2(50) TYPE C.

  AUTHORITY-CHECK OBJECT 'M_MATE_STA'
           ID 'ACTVT' FIELD '03'
           ID 'STATM' FIELD SPACE.
  IF SY-SUBRC NE 0.
    RAISE NO_PERMISSION.
  ENDIF.

  PERFORM ITEM_AVAILABLE_CHECK TABLES   CT_WORKTAB
                               CHANGING LF_FLAG_ITEMS.
  IF LF_FLAG_ITEMS EQ SPACE.
    RAISE NO_ITEM_DATA.
  ENDIF.

  LOOP AT CT_WORKTAB.
    LT_SELECT-MATNR = CT_WORKTAB-MATNR.
    LT_SELECT-VKORG = CT_WORKTAB-VKORG.
    LT_SELECT-VTWEG = CT_WORKTAB-VTWEG.
    COLLECT LT_SELECT.
  ENDLOOP.

  LOOP AT LT_SELECT.
    IF SY-TABIX GT 1.
      LF_TEXTLINE1 = 'NÃ¤chstes Material &1 anzeigen?'(007).
      LF_TEXTLINE2 = 'Verkaufsorganisation &1 Vertriebsweg &2'(009).
      REPLACE '&1' WITH LT_SELECT-MATNR INTO LF_TEXTLINE1.
      REPLACE '&1' WITH LT_SELECT-VKORG INTO LF_TEXTLINE2.
      REPLACE '&2' WITH LT_SELECT-VTWEG INTO LF_TEXTLINE2.
      CONDENSE LF_TEXTLINE1.
      CONDENSE LF_TEXTLINE2.
      CALL FUNCTION 'POPUP_TO_CONFIRM_STEP'
           EXPORTING
*               DEFAULTOPTION  = 'Y'
                TEXTLINE1      = LF_TEXTLINE1
                TEXTLINE2      = LF_TEXTLINE2
                TITEL          = 'Materialstamm anzeigen'(008)
*               START_COLUMN   = 25
*               START_ROW      = 6
*               CANCEL_DISPLAY = 'X'
           IMPORTING
                ANSWER         = LF_ANSWER.
      CASE LF_ANSWER.
        WHEN 'N'.
          CONTINUE.
        WHEN 'A'.
          EXIT.
      ENDCASE.
    ENDIF.
    SET PARAMETER ID 'MXX' FIELD 'V'.
    SET PARAMETER ID 'MAT' FIELD LT_SELECT-MATNR.
    SET PARAMETER ID 'VKO' FIELD LT_SELECT-VKORG.
    SET PARAMETER ID 'VTW' FIELD LT_SELECT-VTWEG.
    CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
  ENDLOOP.

ENDFUNCTION.
